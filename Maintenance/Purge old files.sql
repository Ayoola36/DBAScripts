-- =============================================
-- Author:		Andy Sargeant
-- Create date:	20/04/2008
-- Description:	Code to purge files with a particular
--				extension created more than X hours ago
--				from a directory and all its first-level
--				subdirectories.  Useful within a SQL
--				Server 2000 environment.
-- =============================================

SET DATEFORMAT ymd

DECLARE	@ROOT_PATH NVARCHAR(260)
		,@FILE_EXTENSION NVARCHAR(256)
		,@HOURS INT
SELECT	@ROOT_PATH = N'\\MONITORINGSERVE\SQLBackup\DCSSERVER'
		,@FILE_EXTENSION = N'BAK'
		,@HOURS = 47

IF OBJECT_ID('dbo.#SHELL_OUT') IS NOT NULL
		DROP TABLE
				dbo.#SHELL_OUT
CREATE TABLE
		dbo.#SHELL_OUT
(
		LINE_NUMBER INT IDENTITY(1,1) NOT NULL
		,SHELL_OUT NVARCHAR(512) NULL
)

DECLARE	@SHELL_CMD NVARCHAR(512)
SELECT	@SHELL_CMD = N'DIR /A:D /B /O:N "' + @ROOT_PATH + N'"'
INSERT	dbo.#SHELL_OUT
EXEC	master.dbo.xp_cmdshell
		@SHELL_CMD

DECLARE	@DIRECTORIES TABLE
(
		DIRECTORY_ID INT IDENTITY(1,1) NOT NULL
		,DIRECTORY_PATH NVARCHAR(260) NOT NULL
)

INSERT	@DIRECTORIES
SELECT	@ROOT_PATH
UNION ALL
SELECT	@ROOT_PATH + N'\' + SHELL_OUT
FROM	dbo.#SHELL_OUT
WHERE	SHELL_OUT IS NOT NULL

DECLARE	@DIRECTORY_ID INT
		,@MAX_DIRECTORY_ID INT
SELECT	@DIRECTORY_ID = MIN(DIRECTORY_ID)
		,@MAX_DIRECTORY_ID = MAX(DIRECTORY_ID)
FROM	@DIRECTORIES

DECLARE	@DIRECTORY_PATH NVARCHAR(260)
		,@CUTOFF_DATE DATETIME
SELECT	@CUTOFF_DATE = DATEADD(hh, -@HOURS, LEFT(CONVERT(NVARCHAR, GETDATE(), 121), 17) + N'00.000')

DECLARE	@FILES TABLE
(
		FILE_ID INT IDENTITY(1,1) NOT NULL
		,FILE_PATH NVARCHAR(260) NOT NULL
)

WHILE	@DIRECTORY_ID <= @MAX_DIRECTORY_ID
BEGIN
		SELECT	@DIRECTORY_PATH = DIRECTORY_PATH
		FROM	@DIRECTORIES
		WHERE	DIRECTORY_ID = @DIRECTORY_ID

		SELECT	@SHELL_CMD = N'DIR /A:-D /N /O:D /T:C /4 "' + @DIRECTORY_PATH + N'\*.' + @FILE_EXTENSION + N'"'
		TRUNCATE TABLE
				dbo.#SHELL_OUT
		INSERT	dbo.#SHELL_OUT
		EXEC	master.dbo.xp_cmdshell
				@SHELL_CMD

		INSERT	@FILES
		SELECT	@DIRECTORY_PATH + N'\' + RIGHT(SHELL_OUT, LEN(SHELL_OUT) - 36)
		FROM	dbo.#SHELL_OUT
				SHELL_OUT
		WHERE	LINE_NUMBER > 5
				AND SUBSTRING(SHELL_OUT, 18, 7) <> N'File(s)'
				AND SUBSTRING(SHELL_OUT, 18, 6) <> N'Dir(s)'
				AND SHELL_OUT <> N'File Not Found'
				AND (SUBSTRING(SHELL_OUT, 7, 4) + N'-' + SUBSTRING(SHELL_OUT, 4, 2) + N'-' +
					LEFT(SHELL_OUT, 2) + N' ' + SUBSTRING(SHELL_OUT, 13, 5) + N':00.000') <
					@CUTOFF_DATE

		SELECT	@DIRECTORY_ID = @DIRECTORY_ID + 1
END

DECLARE	@FILE_ID INT
		,@MAX_FILE_ID INT
SELECT	@FILE_ID = MIN(FILE_ID)
		,@MAX_FILE_ID = MAX(FILE_ID)
FROM	@FILES

DECLARE	@FILE_PATH NVARCHAR(260)
		,@FILE_LIST NVARCHAR(4000)
SELECT	@FILE_LIST = N''

WHILE	@FILE_ID <= @MAX_FILE_ID
BEGIN
		SELECT	@FILE_PATH = FILE_PATH
		FROM	@FILES
		WHERE	FILE_ID = @FILE_ID

		SELECT	@SHELL_CMD = N'CMD /C DEL "' + @FILE_PATH + N'"'
		EXEC	master.dbo.xp_cmdshell
				@SHELL_CMD
				,no_output

		SELECT	@FILE_LIST = @FILE_LIST + N'
' + @FILE_PATH

		SELECT	@FILE_ID = @FILE_ID + 1
END

PRINT	N'The following files were purged:
' + @FILE_LIST
