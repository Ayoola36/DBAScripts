DECLARE	@TABLES TABLE
(
		TABLE_ID INT IDENTITY(1, 1) PRIMARY KEY
		,TABLE_NAME NVARCHAR(128) NOT NULL
		,IS_EMPTY VARCHAR(3) NULL
)

INSERT	@TABLES
SELECT	N'[' + TABLE_SCHEMA + N'].[' + TABLE_NAME + N']' AS TABLE_NAME
		,NULL
FROM	INFORMATION_SCHEMA.TABLES
WHERE	TABLE_TYPE = 'BASE TABLE'

DECLARE	@TABLE_ID INT
		,@MAX_TABLE_ID INT
		,@SQL NVARCHAR(255)
		,@PARM_DEFINITION NVARCHAR(255)
		,@IS_EMPTY BIT

SELECT	@TABLE_ID = 1
		,@MAX_TABLE_ID = MAX(TABLE_ID)
		,@PARM_DEFINITION = N'@IS_EMPTY BIT OUTPUT'
FROM	@TABLES

WHILE	@TABLE_ID <= @MAX_TABLE_ID
BEGIN
		SELECT	@SQL = N'IF EXISTS (SELECT TOP 1 * FROM ' + TABLE_NAME + ') SET @IS_EMPTY = 0 ELSE SET @IS_EMPTY = 1'
		FROM	@TABLES
		WHERE	TABLE_ID = @TABLE_ID

		EXEC	sp_executesql
				@SQL
				,@PARM_DEFINITION
				,@IS_EMPTY = @IS_EMPTY OUTPUT

		UPDATE	@TABLES
		SET		IS_EMPTY = CASE @IS_EMPTY WHEN 0 THEN 'No' ELSE 'Yes' END
		WHERE	TABLE_ID = @TABLE_ID

		SET		@TABLE_ID = @TABLE_ID + 1
END

SELECT	TABLE_ID
		,TABLE_NAME
		,IS_EMPTY
FROM	@TABLES
ORDER BY
		TABLE_NAME
